plugins {
    id "scala"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.1"
    id "com.diffplug.gradle.spotless" version "3.28.1"
}

group = "pro.civitaspo"
version = "0.0.2"
description = "An input plugin for Embulk (https://github.com/embulk/embulk/) that" +
    " unions all data loaded by your defined embulk input & filters plugin configuration."

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.23"
    // NOTE: Is shadow plugin required in the future?
    compile "org.scala-lang:scala-library:2.13.1"

    testImplementation "org.embulk:embulk-deps-buffer:0.9.23"
    testImplementation "org.embulk:embulk-deps-config:0.9.23"
    testImplementation "org.embulk:embulk-core:0.9.23"
    testImplementation "org.embulk:embulk-core:0.9.23:tests"
    testImplementation "org.embulk:embulk-standards:0.9.23"
    testImplementation "org.scalatest:scalatest_2.13:3.1.1"
}

embulkPlugin {
    mainClass = "pro.civitaspo.embulk.input.union.UnionInputPlugin"
    category = "input"
    type = "union"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}

spotless {
    scala {
        scalafmt('2.4.2').configFile('.scalafmt.conf')
    }
}

gem {
    from("LICENSE.txt")
    authors = ["Civitaspo"]
    email = [ "civitaspo@gmail.com" ]
    summary = "An embulk input plugin to union all loaded data."
    homepage = "https://github.com/civitaspo/embulk-input-union"
    licenses = ["MIT"]
}

gemPush {
    host = "https://rubygems.org"
}

task scalatest(dependsOn: ['testClasses'], type: JavaExec) {
    main = 'org.scalatest.tools.Runner'
    args = ['-R', 'build/classes/scala/test', '-o']
    classpath = sourceSets.test.runtimeClasspath
}
